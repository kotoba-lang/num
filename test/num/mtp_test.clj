(ns num.mtp-test
  (:require [clojure.test :refer [deftest is testing]]
            [num.array :as arr]))

(def target {:shape [4 8] :backend :device :dtype :f32})
(def draft {:shape [4 8] :backend :device :dtype :f32})

(deftest verifies-an-mtp-prefix-and-stops-on-correction
  (let [rows (atom [])]
    (with-redefs [arr/speculative-rejection-row
                  (fn [_target _draft row token _options]
                    (swap! rows conj row)
                    (if (= row 2)
                      {:accepted? false :token 7}
                      {:accepted? true :token token}))]
      (is (= {:tokens [1 2 7]
              :drafted 4
              :accepted 2
              :all-accepted? false}
             (arr/speculative-rejection-rows
              target draft [1 2 3 4]
              {:temperature 0.7
               :acceptance-random 0.5
               :residual-random 0.5})))
      (is (= [0 1 2] @rows)))))

(deftest accepts-the-complete-mtp-proposal
  (with-redefs [arr/speculative-rejection-row
                (fn [_target _draft _row token _options]
                  {:accepted? true :token token})]
    (is (= {:tokens [1 2 3 4]
            :drafted 4
            :accepted 4
            :all-accepted? true}
           (arr/speculative-rejection-rows
            target draft [1 2 3 4]
            (repeat 4 {:temperature 1.0}))))))

(deftest rejects-misaligned-proposals-before-dispatch
  (testing "the draft head may not claim more tokens than it supplied options for"
    (is (thrown-with-msg?
         clojure.lang.ExceptionInfo
         #"one option and logit row"
         (arr/speculative-rejection-rows target draft [1 2] [{}])))))
