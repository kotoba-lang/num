(ns num.dtype
  "IEEE fp16/bfloat16 scalar conversion used by reference typed storage.")

(def supported #{:f32 :f16 :bf16})

(defn check [dtype]
  (when-not (contains? supported dtype)
    (throw (ex-info "unsupported dtype" {:dtype dtype :supported supported})))
  dtype)

#?(:cljs (defonce ^:private conversion-view (js/DataView. (js/ArrayBuffer. 4))))

(defn- float-bits [x]
  #?(:clj (Float/floatToRawIntBits (unchecked-float x))
     :cljs (do (.setFloat32 conversion-view 0 x true)
               (.getUint32 conversion-view 0 true))))

(defn- bits-float [x]
  #?(:clj (double (Float/intBitsToFloat (unchecked-int x)))
     :cljs (do (.setUint32 conversion-view 0 x true)
               (.getFloat32 conversion-view 0 true))))

(defn- bits16 [x]
  #?(:clj (unchecked-short x)
     :cljs (let [v (bit-and x 0xffff)] (if (> v 0x7fff) (- v 0x10000) v))))

(defn f32->f16-bits [x]
  (let [bits (float-bits x)
        sign (bit-and (unsigned-bit-shift-right bits 16) 0x8000)
        exponent (- (bit-and (unsigned-bit-shift-right bits 23) 0xff) 127 -15)
        mantissa (bit-and bits 0x7fffff)]
    (cond
      (= exponent 143) (bits16 (bit-or sign (if (zero? mantissa) 0x7c00 0x7e00)))
      (>= exponent 31) (bits16 (bit-or sign 0x7c00))
      (<= exponent -10) (bits16 sign)

      (<= exponent 0)
      (let [m (bit-or mantissa 0x800000)
            shift (- 14 exponent)
            halfway (bit-shift-left 1 (dec shift))
            rounded (+ m (dec halfway)
                       (bit-and (unsigned-bit-shift-right m shift) 1))]
        (bits16 (bit-or sign (unsigned-bit-shift-right rounded shift))))

      :else
      (let [rounded (+ mantissa 0xfff
                       (bit-and (unsigned-bit-shift-right mantissa 13) 1))
            carry? (not (zero? (bit-and rounded 0x800000)))
            exponent* (if carry? (inc exponent) exponent)
            mantissa* (if carry? 0 rounded)]
        (if (>= exponent* 31)
          (bits16 (bit-or sign 0x7c00))
          (bits16 (bit-or sign (bit-shift-left exponent* 10)
                              (unsigned-bit-shift-right mantissa* 13))))))))

(defn f16-bits->f32 [x]
  (let [h (bit-and (int x) 0xffff)
        sign (bit-shift-left (bit-and h 0x8000) 16)
        exponent (bit-and h 0x7c00)
        mantissa (bit-and h 0x03ff)]
    (cond
      (= exponent 0x7c00)
      (bits-float (bit-or sign 0x7f800000 (bit-shift-left mantissa 13)))

      (zero? exponent)
      (if (zero? mantissa)
        (bits-float sign)
        (loop [m mantissa e -14]
          (if (zero? (bit-and m 0x400))
            (recur (bit-shift-left m 1) (dec e))
            (bits-float (bit-or sign (bit-shift-left (+ e 127) 23)
                                (bit-shift-left (bit-and m 0x3ff) 13))))))

      :else
      (bits-float (bit-or sign
                          (bit-shift-left (+ (unsigned-bit-shift-right exponent 10) 112) 23)
                          (bit-shift-left mantissa 13))))))

(defn f32->bf16-bits [x]
  (let [bits (float-bits x)
        rounded (+ bits 0x7fff (bit-and (unsigned-bit-shift-right bits 16) 1))]
    (bits16 (unsigned-bit-shift-right rounded 16))))

(defn bf16-bits->f32 [x]
  (bits-float (bit-shift-left (bit-and (int x) 0xffff) 16)))

(defn element-bytes [dtype]
  (case (check dtype) :f32 4 (:f16 :bf16) 2))
